" vim: set sw=2 ts=2 sts=2 et tw=78 foldmarker={{,}} foldmethod=marker:
" @author: Haoming Wang <haoming.exe@gmail.com>

" let g:noplug = 1

" Environment {{

" Basics {{

set nocompatible        " Must be first line
augroup vimrc
  autocmd!
augroup END

" }}

" Identify platform {{

let s:mac = has('macunix')
let s:linux = has('unix') && !has('macunix') && !has('win32unix')
let s:win = has('win32')

" }}

" Windows Compatible {{

" On Windows, also use '.vim' instead of 'vimfiles';
" this makes synchronization across (heterogeneous) systems easier.
if s:win
  set runtimepath=$HOME/.vim,$VIM/vimfiles,$VIMRUNTIME
endif

" }}

" }} // namespace environment

" Bundles {{

if has('nvim')
  if s:linux
    let g:python_host_prog = '/usr/bin/python2'
    let g:python3_host_prog = '/usr/bin/python3'
  else
    let g:python3_host_prog = '/usr/local/bin/python3'
  endif
endif

if !exists('g:noplug')

" list only the plugin groups you will use
if !exists('g:plug_groups')
  let g:plug_groups = ['general', 'editing', 'programming']
endif

call plug#begin('~/.vim/bundle')

" General {{

if count(g:plug_groups, 'general')
  Plug 'romainl/vim-cool'
  Plug 'junegunn/goyo.vim'
  Plug 'junegunn/limelight.vim'
  Plug 'itchyny/lightline.vim'
  Plug 'haomingw/vim-startscreen'
  Plug 'rafi/awesome-vim-colorschemes'
  Plug 'junegunn/fzf.vim'
  Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
endif

" }}

" Editing {{

if count(g:plug_groups, 'editing')
  Plug 'chrisbra/csv.vim'
  Plug 'tpope/vim-repeat'
  Plug 'tpope/vim-endwise'
  Plug 'tpope/vim-surround'
  Plug 'rhysd/clever-f.vim'
  Plug 'junegunn/vim-easy-align'
  Plug 'easymotion/vim-easymotion'
  Plug 'terryma/vim-expand-region'
  Plug 'michaeljsmith/vim-indent-object'
  Plug 'christoomey/vim-tmux-navigator'
  Plug 'mbbill/undotree', { 'on': 'UndotreeToggle' }
  Plug 'scrooloose/nerdtree', { 'on': 'NERDTreeToggle' }
  if !has('nvim')
    Plug 'machakann/vim-highlightedyank'
  endif
endif

" }}

" Programming {{

if count(g:plug_groups, 'programming')
  Plug 'fatih/vim-go'
  Plug 'keith/swift.vim'
  Plug 'rust-lang/rust.vim'
  Plug 'dense-analysis/ale'
  Plug 'mhinz/vim-signify'
  Plug 'tpope/vim-fugitive'
  Plug 'junegunn/gv.vim'
  Plug 'ruanyl/vim-gh-line'
  Plug 'kdheepak/lazygit.nvim'
  Plug 'rhysd/git-messenger.vim'
  Plug 'scrooloose/nerdcommenter'
  Plug 'AndrewRadev/splitjoin.vim'
  Plug 'haomingw/vim-snippets'
  Plug 'jeetsukumaran/vim-indentwise'
  Plug 'junegunn/rainbow_parentheses.vim'
  Plug 'sbdchd/neoformat'
  Plug 'nathangrigg/vim-beancount', { 'for': 'beancount' }
  Plug 'SirVer/ultisnips', { 'on': [] }
  Plug 'majutsushi/tagbar', { 'on': 'TagbarToggle' }
  Plug 'neoclide/coc.nvim',
    \ executable('node') ? { 'branch': 'release' } : { 'on': [] }
  Plug 'Yggdroot/indentLine', { 'on': 'IndentLinesToggle' }
endif

" }}

call plug#end()

endif

" }} // namespace bundles

" General {{

set mouse=a                     " Automatically enable mouse usage
set mousehide                   " Hide the mouse cursor while typing
scriptencoding utf-8

if has('clipboard')
  if has('unnamedplus')         " When possible use + register for copy-paste
    set clipboard=unnamed,unnamedplus
  else
    " On mac and Windows, use * register for copy-paste
    set clipboard=unnamed
  endif
endif

if !exists('g:xming_no_restore_cursor')
  function! ResCur()
    if line("'\"") <= line("$")
      silent! normal! g`"
      return 1
    endif
  endfunction

  augroup resCur
    autocmd!
    autocmd BufWinEnter * call ResCur()
  augroup END
endif

set shortmess=at                " Abbrev. of messages (avoids 'hit enter')
" Better Unix / Windows compatibility
set viewoptions=folds,options,cursor,unix,slash
set virtualedit=block
set history=1000
set tabpagemax=50
set hidden                      " Allow buffer switching without saving
set iskeyword-=.                " '.' is an end of word designator
set iskeyword-=#                " '#' is an end of word designator
set iskeyword-=-                " '-' is an end of word designator

set nobackup noswapfile nowritebackup
if has('persistent_undo')
  set undofile                " So is persistent undo ...
  set undodir=$HOME/.vim/undo
  " Maximum number of changes that can be undone
  set undolevels=1000
  " Maximum number lines to save for undo on a buffer reload
  set undoreload=10000
endif

set nrformats-=octal
set complete-=i                 " Searching includes can be slow
set updatetime=300
set ruler
set formatoptions+=j            " Delete comment character when joining commented lines
set autoread
set textwidth=0
if has('nvim')
  set lazyredraw
endif

" }} // namespace general

" Vim UI {{

try
  color gruvbox
  if exists('+termguicolors')
    let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
    let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
    set termguicolors           " enable true colors support
  endif
catch
  color desert
endtry

set background=dark
if !has('gui_running')
  highlight Normal ctermbg=NONE guibg=NONE
endif

set relativenumber
set number                      " Line numbers on
set signcolumn=number
if !exists('g:noplug')
  set cursorline                " Highlight current line
  set noshowmode                " Don't display the current mode
endif
set confirm
set showcmd                     " Show partial commands in status line
set vb t_vb=                    " Shut up bell
set backspace=indent,eol,start  " Backspace for dummies
set linespace=0                 " No extra spaces between rows
set showmatch                   " Show matching brackets/parenthesis
set incsearch                   " Find as you type search
set hlsearch                    " Highlight search terms
set winminheight=0              " Windows can be 0 line high
set ignorecase                  " Case insensitive search
set smartcase                   " Case sensitive when uppercase letter present
set wildmenu                    " Show list instead of just completing
set wildmode=full               " Command <Tab> completion, list matches
set wildignore+=*.o,*.so,*.pyc,*/__pycache__/,.git/*,.DS_Store
set whichwrap=b,s,h,l,<,>,[,]   " Backspace and cursor keys wrap too
set scrolljump=5                " Lines to scroll when cursor leaves screen
set scrolloff=3                 " Minimum lines to keep above and below cursor
set foldenable                  " Auto fold code
set list
set laststatus=2                " Display status line
set sessionoptions-=options
set viewoptions-=options

" Highlight problematic whitespace
set listchars=tab:›\ ,trail:•,extends:#,nbsp:.

" Highlight a selection on yank
au TextYankPost * silent! lua vim.highlight.on_yank()

" }} // namespace vim-ui

" Formatting {{

if !exists('g:noplug')
  set nowrap
endif
set linebreak                   " Avoid wrapping a line in the middle of a word
set autoindent                  " copy indent from previous line
set smartindent                 " enable nice indent

set shiftwidth=2                " Use indents of 2 spaces
set tabstop=2                   " An indentation every four columns
set softtabstop=2               " Let backspace delete indent
set expandtab                   " Tabs are spaces, not tabs
set smarttab
set shiftround                  " Round the indentation to the nearest multiple of “shiftwidth.”

set splitright splitbelow
set pastetoggle=<F12>           " pastetoggle (sane indentation on pastes)
set nojoinspaces                " Prevents inserting two spaces after punctuation on a join (J)

" }} // namespace formatting

" Key (re)Mappings {{

" The default leader is '\', but many people prefer ' ' or ','
" as it's in a standard location.
let mapleader = ' '
let maplocalleader = ','

if !has('gui_running')
  try
    " fix meta-keys which generate <Esc>a .. <Esc>z
    let c = 'a'
    while c <= 'z'
      exec "set <A-".c.">=\e".c
      let c = nr2char(1+char2nr(c))
    endw
    set ttimeout ttimeoutlen=50
  catch
    " neovim
  endtry
endif

" qq to record, Q to replay
nnoremap Q @q

" Wrapped lines goes down/up to next row, rather than next line in file.
nnoremap j gj
nnoremap k gk

nnoremap n nzzzv
nnoremap N Nzzzv

nnoremap }  }zz
nnoremap {  {zz
nnoremap ]] ]]zz
nnoremap [[ [[zz
nnoremap [] []zz
nnoremap ][ ][zz

" Allow to trigger background
function! ToggleBG()
  let s:tbg = &background
  " Inversion
  if s:tbg == "dark"
    set background=light
  else
    set background=dark
  endif
endfunction
nnoremap <leader>bg :call ToggleBG()<cr>
nnoremap <leader>bt :%retab!<cr>
nnoremap <leader>bi gg=G<c-o>

" Visual shifting (does not exit Visual mode)
vnoremap < <gv
vnoremap > >gv

cnoremap %% <c-r>=fnameescape(expand('%:h')).'/'<cr>
map <leader>ee :e %%
map <leader>es :sp %%
map <leader>ev :vsp %%
map <leader>et :tabe %%
map <leader>rp :%s/

command! VimConfig :e $MYVIMRC
map <leader>rl :source $MYVIMRC<cr>

" split new buffer
map <silent> <leader>ns :new<cr>
map <silent> <leader>nv :vnew<cr>

" Switch between tabs
if s:mac
  nnoremap ˙ gT
  nnoremap ¬ gt
else
  nnoremap <a-h> gT
  nnoremap <a-l> gt
endif

nnoremap [t :tabp<cr>
nnoremap ]t :tabn<cr>

" buffers
nnoremap [b :bprev<cr>
nnoremap ]b :bnext<cr>

" Move cursor in Insert mode
if s:mac
  inoremap ˙ <Left>
  inoremap ∆ <Down>
  inoremap ˚ <Up>
  inoremap ¬ <Right>
else
  inoremap <a-h> <Left>
  inoremap <a-j> <Down>
  inoremap <a-k> <Up>
  inoremap <a-l> <Right>
endif

" Move tabs
if s:mac
  nnoremap “ :call xming#tabmove(-1)<cr>
  nnoremap ‘ :call xming#tabmove(1)<cr>
else
  nnoremap <a-[> :call xming#tabmove(-1)<cr>
  nnoremap <a-]> :call xming#tabmove(1)<cr>
endif

" Emacs/terminal compatible

"" begin & end
inoremap <c-e> <End>
inoremap <c-a> <c-o>I
cnoremap <c-a> <Home>

"" move cursor by word
inoremap <a-b> <c-o>b
inoremap <a-f> <c-o>w
if s:mac
  inoremap ∫ <c-o>b
  inoremap ƒ <c-o>w
endif

"" delete forward & backward, <ctrl-h> mapped by default
inoremap <c-d> <Delete>
cnoremap <c-d> <Delete>

"" various deletes
inoremap <c-k> <c-o>D
cnoremap <c-k> <c-\>e(strpart(getcmdline(), 0, getcmdpos() - 1))<cr>
"" undoable
inoremap <c-u> <c-g>u<c-u>
inoremap <c-w> <c-g>u<c-w>

" Window:
nnoremap <c-h> <c-w>h
nnoremap <c-l> <c-w>l
nnoremap <c-k> <c-w>k
nnoremap <c-j> <c-w>j

" Resize panes
nnoremap <leader>= <c-w>=
nnoremap <leader>+ <c-w>5+
nnoremap <leader>- <c-w>5-
nnoremap <leader>> <c-w>5>
nnoremap <leader>< <c-w>5<

" Terminal
tnoremap <Esc> <c-\><c-n>
tnoremap <c-h> <c-\><c-N><c-w>h
tnoremap <c-j> <c-\><c-N><c-w>j
tnoremap <c-k> <c-\><c-N><c-w>k
tnoremap <c-l> <c-\><c-N><c-w>l

if has('nvim')
  nnoremap <leader>ts :split +terminal<cr>
  nnoremap <leader>tv :vsplit +terminal<cr>
endif

" Fast editting
nnoremap <c-s> :w<cr>
nnoremap cl ggdG
nnoremap <leader>Y :%y<cr>
inoremap <c-s> <Esc>:w<cr>a
inoremap <c-v> <Esc>pa
inoremap <c-j> <Esc>o
inoremap jj <Esc>O

" utils
nnoremap ; :
nnoremap Y y$
nnoremap <c-Up> <c-y>
nnoremap <c-Down> <c-e>
" yank text in command mode
cnoremap <c-v> <c-r>+
nnoremap <leader>pa :echo expand('%:p')<cr>
nnoremap <leader>pu :PlugUpdate!<cr>
nnoremap <leader>pc :PlugClean!<cr>

" Open new line below and above current line
nnoremap <leader>o o<esc>
nnoremap <leader>O O<esc>

nnoremap [e  :<c-u>execute 'move -1-'. v:count1<cr>
nnoremap ]e  :<c-u>execute 'move +'. v:count1<cr>

iabbrev tab2 vim: set sw=2 ts=2 sts=2 et
iabbrev tab4 vim: set sw=4 ts=4 sts=4 et
iabbrev #! #!/usr/bin/env

xnoremap @ :<c-u>call ExecuteMacroOverVisualRange()<cr>

" }} // namespace remapping

" Autocmd {{

augroup vimrc
  " Only enable relative number in normal mode
  autocmd InsertEnter * :set norelativenumber
  autocmd InsertLeave * :set relativenumber

  " Adjust viewports to the same size
  autocmd vimResized * exe "normal! \<c-w>="

  " Strip Trailing Whitespaces On Saving:
  " The 'e' flag to not give an error if there is no match
  autocmd BufWritePre *
    \ if &ft != 'markdown' && &ft != 'tex' | :silent %s/\s\+$//e | endif

  " File type settings
  autocmd FileType c :call C_setup()
  autocmd FileType cpp :call Cpp_setup()
  autocmd FileType objc :call Objc_setup()
  autocmd FileType python :call Python_setup()
  autocmd FileType sh :call Bash_setup()
  autocmd FileType swift :call Swift_setup()
augroup END

" }} // namespace autocmd

" Plugins {{

if !exists('g:noplug')

" Ultisnips {{

if has_key(g:plugs, 'ultisnips')
  " Trigger configuration. Do not use <tab>
  let g:UltiSnipsExpandTrigger = "<c-l>"
  let g:UltiSnipsJumpForwardTrigger="<c-l>"
  " If you want :UltiSnipsEdit to split your window.
  let g:UltiSnipsEditSplit = "vertical"
  command! LoadSnips call plug#load('ultisnips')
endif

" }}

" Vim-snippets {{

if has_key(g:plugs, 'vim-snippets')
  let g:snips_author = $USER
endif

" }}

" fzf.vim {{

if has_key(g:plugs, 'fzf.vim')
  if has('nvim-0.4.3')
    let g:fzf_layout = { 'window': { 'width': 0.9, 'height': 0.6 } }
  endif
  let g:fzf_colors = {
  \ 'fg':      ['fg', 'Normal'],
  \ 'bg':      ['bg', 'Normal'],
  \ 'hl':      ['fg', 'Comment'],
  \ 'fg+':     ['fg', 'CursorLine', 'CursorColumn', 'Normal'],
  \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
  \ 'hl+':     ['fg', 'Statement'],
  \ 'info':    ['fg', 'PreProc'],
  \ 'border':  ['fg', 'Ignore'],
  \ 'prompt':  ['fg', 'Conditional'],
  \ 'pointer': ['fg', 'Exception'],
  \ 'marker':  ['fg', 'Keyword'],
  \ 'spinner': ['fg', 'Label'],
  \ 'header':  ['fg', 'Comment']
  \}

  function! RipgrepFzf(query, fullscreen)
    let command_fmt = 'rg --column --line-number --no-heading --color=always --smart-case %s || true'
    let initial_command = printf(command_fmt, shellescape(a:query))
    let reload_command = printf(command_fmt, '{q}')
    let options = {'options': ['--phony', '--query', a:query, '--bind', 'change:reload:'.reload_command]}
    if a:fullscreen
      let options = fzf#vim#with_preview(options)
    endif
    call fzf#vim#grep(initial_command, 1, options, a:fullscreen)
  endfunction

  command! -nargs=* -bang RG call RipgrepFzf(<q-args>, <bang>0)

  nnoremap <silent> <expr> <c-p> (expand('%') =~ 'NERD_tree' ? "\<c-w>\<c-w>" : '').":Files\<cr>"
  nnoremap <silent> <leader>rg :RG<cr>
endif

" }}

" Goyo {{

if has_key(g:plugs, 'goyo.vim')
  nnoremap <leader>gy :Goyo<cr>
endif

" }}

" Limelight {{

if has_key(g:plugs, 'limelight.vim')
  " Number of preceding/following paragraphs to include (default: 0)
  let g:limelight_paragraph_span = 1
  let g:limelight_priority = -1

  " Color name (:help cterm-colors) or ANSI code
  let g:limelight_conceal_ctermfg = 'gray'
  let g:limelight_conceal_ctermfg = 240

  " Color name (:help gui-colors) or RGB color
  let g:limelight_conceal_guifg = 'DarkGray'
  let g:limelight_conceal_guifg = '#777777'

  autocmd! User GoyoEnter Limelight
  autocmd! User GoyoLeave Limelight!
endif

" }}

" Lightline {{

if has_key(g:plugs, 'lightline.vim')
  let g:lightline = {
  \ 'colorscheme': 'gruvbox',
  \ 'active': {
  \   'left': [ ['mode', 'paste'],
  \             ['fugitive', 'readonly', 'filename', 'modified'] ],
  \   'right': [ ['lineinfo'], ['percent'] ]
  \ },
  \ 'tabline' : {
  \   'right' : []
  \ },
  \ 'component': {
  \   'readonly': '%{&filetype=="help"?"":&readonly?"":""}',
  \   'modified': '%{&filetype=="help"?"":&modified?"+":&modifiable?"":"-"}',
  \ },
  \ 'component_function': {
  \   'fugitive': 'LightlineFugitive',
  \ },
  \}

  function! LightlineFugitive()
    if exists('*FugitiveHead')
      let branch = FugitiveHead()
      return branch != '' ? 'ᚠ '.branch : ''
    endif
    return ''
  endfunction
endif

" }}

" Vim-easy-align {{

if has_key(g:plugs, 'vim-easy-align')
  " Start interactive EasyAlign in visual mode (e.g. vipga)
  xmap ga <Plug>(EasyAlign)

  " Start interactive EasyAlign for a motion/text object (e.g. gaip)
  nmap ga <Plug>(EasyAlign)
endif

" }}

" Vim-expand-region {{

if has_key(g:plugs, 'vim-expand-region')
  xmap L <Plug>(expand_region_expand)
  xmap H <Plug>(expand_region_shrink)
endif

" }}

" Vim-highlightedyank {{

if has_key(g:plugs, 'vim-highlightedyank')
  if !exists('##TextYankPost')  " for older vim
    map y <Plug>(highlightedyank)
  endif
  let g:highlightedyank_highlight_duration = 100
  highlight HighlightedyankRegion cterm=reverse gui=reverse
endif

" }}

" Vim-indent-object {{

" <count>ai   An Indentation level and line above.
" <count>ii   Inner Indentation level (no line above).
" <count>aI   An Indentation level and lines above/below.
" <count>iI   Inner Indentation level (no lines above/below).

" }}

" Undotree {{

if has_key(g:plugs, 'undotree')
  let g:undotree_WindowLayout = 2
  nnoremap U :UndotreeToggle<cr>
endif

" }}

" Nerdtree {{

if has_key(g:plugs, 'nerdtree')
  nnoremap <leader>nt :NERDTreeToggle<cr>
  let g:NERDTreeDirArrowExpandable = '+'
  let g:NERDTreeDirArrowCollapsible = '-'
  augroup vimrc
    " close vim if the only window left open is a NERDTree
    autocmd bufenter *
      \ if (winnr("$") == 1 && exists("b:NERDTree") &&
      \ b:NERDTree.isTabTree()) | q | endif
  augroup END
  let NERDTreeIgnore = ['\.o$', '\.pyc$', '^__pycache__$']
endif

" }}

" RainbowParentheses {{

if has_key(g:plugs, 'rainbow_parentheses.vim')
  " Activation based on file type
  augroup vimrc
    autocmd FileType c,cpp,python,sh RainbowParentheses
  augroup END
  let g:rainbow#pairs = [['(', ')'], ['[', ']']]
endif

" }}

" Neoformat {{

if has_key(g:plugs, 'neoformat')
  map <leader>f :Neoformat<cr>
endif

" }}

" NerdCommenter {{

if has_key(g:plugs, 'nerdcommenter')
  " remap NERDCommenterToggle
  nmap <c-_> <leader>c<space>j
  xmap <c-_> <leader>c<space>j

  " Add spaces after comment delimiters by default
  let g:NERDSpaceDelims = 1
  " Allow commenting and inverting empty lines
  " (useful when commenting a region)
  let g:NERDCommentEmptyLines = 1
  " Align line-wise comment delimiters flush left
  " instead of following code indentation
  let g:NERDDefaultAlign = 'left'
endif

" }}

" Tagbar {{

if has_key(g:plugs, 'tagbar')
  let g:tagbar_sort = 0
  nnoremap <leader>tb :TagbarToggle<cr>
endif

"}}

" Vim-signify {{

if has_key(g:plugs, 'vim-signify')
  let g:signify_vcs_list = ['git']
  let g:signify_skip_filetype = { 'journal': 1 }
endif

"}}

" Fugitive {{

if has_key(g:plugs, 'vim-fugitive')
  nnoremap <silent> <leader>gd :Git diff<cr>
  nnoremap <silent> <leader>gc :Git commit<cr>
  nnoremap <silent> <leader>gb :Git blame<cr>
  nnoremap <silent> <leader>gp :Git push<cr>
  nnoremap <silent> <leader>gr :Gread<cr>
  nnoremap <silent> <leader>gw :Gwrite<cr>
  nnoremap <silent> <leader>ge :Gedit<cr>
  " Mnemonic _i_nteractive
  nnoremap <silent> <leader>ga :Git add -p %<cr>
  nnoremap <silent> <leader>gs :SignifyToggle<cr>
endif

"}}

" gv.vim {{

if has_key(g:plugs, 'gv.vim')
  nnoremap <silent> <leader>gl :GV<cr>
endif

"}}

" GitMessenger {{

if has_key(g:plugs, 'git-messenger.vim')
  function! s:setup_git_messenger_popup() abort
    " For example, set go back/forward history to <C-o>/<C-i>
    nmap <buffer><C-o> o
    nmap <buffer><C-i> O
  endfunction
  augroup gitmessenger
    autocmd!
    autocmd FileType gitmessengerpopup :call <SID>setup_git_messenger_popup()
  augroup END
endif

"}}

" Vim-gh-line {{

if has_key(g:plugs, 'vim-gh-line')
  let g:gh_line_map_default = 0
  let g:gh_line_blame_map_default = 0

  let g:gh_line_map = '<leader>gh'
  let g:gh_repo_map = '<leader>go'
endif

"}}

" Lazygit {{

if has_key(g:plugs, 'lazygit.nvim')
  nnoremap <silent> <leader>gg :LazyGit<cr>
endif

"}}

" indentLine {{

if has_key(g:plugs, 'indentLine')
  autocmd! User indentLine doautocmd indentLine Syntax
  let g:indentLine_color_term = 239
  let g:indentLine_color_gui = '#616161'
  nnoremap <leader>il :IndentLinesToggle<cr>
  if has('conceal')
    augroup vimrc
      autocmd FileType c,cpp,python,sh IndentLinesToggle
    augroup END
  endif
endif

" }}

" Easymotion {{

if has_key(g:plugs, 'vim-easymotion')
  let g:EasyMotion_smartcase = 1
  " keep cursor column when JK motion
  let g:EasyMotion_startofline = 0

  map <leader>h <Plug>(easymotion-linebackward)
  map <leader>j <Plug>(easymotion-j)
  map <leader>k <Plug>(easymotion-k)
  map <leader>l <Plug>(easymotion-lineforward)
  map <leader>. <Plug>(easymotion-repeat)
endif

" }}

" Ale {{

if has_key(g:plugs, 'ale')
  let g:ale_echo_msg_error_str = 'E'
  let g:ale_echo_msg_warning_str = 'W'
  let g:ale_echo_msg_format = '[%linter%] %s [%severity%]'

  " Only run linting when saving the file
  let g:ale_lint_on_text_changed = 'never'
  let g:ale_lint_on_insert_leave = 0
  " Don't run linters on opening a file
  let g:ale_lint_on_enter = 0

  " Only run linters named in ale_linters settings.
  let g:ale_linters_explicit = 1
  let g:ale_linters = {
  \ 'cpp': 'all',
  \ 'python': ['flake8', 'mypy', 'pylint'],
  \ 'go': ['gofmt', 'golint', 'govet'],
  \ 'rust': ['cargo'],
  \ 'sh': ['shell'],
  \ 'zsh': ['shell'],
  \ 'markdown': 'all',
  \ 'yaml': 'all',
  \}

  " Fix Python files with autopep8 and yapf.
  let g:ale_fixers = {
  \ 'python': ['autopep8', 'yapf'],
  \}

endif

" }}

" Vim-go {{

if has_key(g:plugs, 'vim-go')
  nnoremap <silent> <leader>gu :GoUpdateBinaries<cr>
  let g:go_fmt_command = "goimports"
  let g:go_highlight_fields = 1
  let g:go_highlight_function_calls = 1
endif

" }}

" Rust.vim {{

if has_key(g:plugs, 'rust.vim')
  let g:rustfmt_autosave = 0
endif

" }}

" coc.nvim {{

if has_key(g:plugs, 'coc.nvim')
  let g:coc_global_extensions = ['coc-pyright', 'coc-clangd', 'coc-rust-analyzer', 'coc-json']

  nmap <silent> gd <Plug>(coc-definition)
  nmap <silent> gi <Plug>(coc-implementation)
  nmap <silent> gr <Plug>(coc-references)

  nmap <leader>rn <Plug>(coc-rename)

  function! s:check_back_space() abort
    let col = col('.') - 1
    return !col || getline('.')[col - 1]  =~# '\s'
  endfunction

  inoremap <silent><expr> <TAB>
    \ pumvisible() ? "\<C-n>" :
    \ <SID>check_back_space() ? "\<TAB>" :
    \ coc#refresh()

  inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

  function! s:show_documentation()
    if (index(['vim', 'help'], &filetype) >= 0)
      execute 'h' expand('<cword>')
    else
      call CocAction('doHover')
    endif
  endfunction

  nnoremap <silent> K :call <SID>show_documentation()<cr>
endif

" }}

endif

" }} // namespace plugins

" GUI Settings {{

" GVIM- (here instead of .gvimrc)
if has('gui_running')
  set guioptions=               " remove all gui options
  set lines=40                  " 40 lines of text instead of 24
  if s:linux
    set guifont=Andale\ Mono\ Regular\ 12,
      \Menlo\ Regular\ 12,
      \Courier\ New\ Regular\ 14
  elseif s:mac
    set guifont=AndaleMono:h12,
      \Menlo-Regular:h12,
      \CourierNewPSMT:h14
  elseif s:win
    set guifont=Consolas:h14:cANSI,
      \Courier_New:h14
  endif
else
  " Enable 256 colors to stop the CSApprox warning and
  " make xterm vim shine
  if &term == 'xterm' || &term == 'screen'
    set t_Co=256
  endif
endif

" }} // namespace gui

" Functions {{

function! Tab2()
  setlocal sw=2 ts=2 sts=2 et
endfunction

function! Tab4()
  setlocal sw=4 ts=4 sts=4 et
endfunction

" Setup C file
function! C_setup()
  iabbrev inc #include
  iabbrev def #define
  iabbrev itn int
  iabbrev mian main

  if !filereadable(getcwd() . "/Makefile")
    let &makeprg='gcc % -DLOCAL -Wall -Wextra -Werror -Wshadow'
  endif

  if s:win
    command! Run :!a.exe
  else
    command! Run :!./a.out
  endif
endfunction

" Setup cpp file
function! Cpp_setup()
  call C_setup()
  iabbrev vi vector<int>
  iabbrev vvi vector<vector<int> >
  syn keyword cppType ll pii

  if !filereadable(getcwd() . "/Makefile")
    let &makeprg='g++ % -DLOCAL -std=c++17 -O2 -Wall -Wextra
      \ -Wpedantic -Wshadow -Wno-unused-result'
  endif
endfunction

" Setup objc file
function! Objc_setup()
  call C_setup()

  if !filereadable(getcwd() . "/Makefile")
    let &makeprg='clang -framework Foundation %'
  endif
endfunction

" Setup python file
function! Python_setup()
  iabbrev im import
  syn keyword pythonDecorator self
  let &makeprg='python %'
endfunction

" Setup bash file
function! Bash_setup()
  let &makeprg='bash %'
endfunction

" Setup Swift file
function! Swift_setup()
  let &makeprg='swiftc %'
endfunction

function! OnBattery()
  if s:mac
    return match(system('pmset -g batt'), \
      "Now drawing from 'Battery Power'") != -1
  elseif s:linux
    return readfile('/sys/class/power_supply/AC/online') == ['0']
  endif
  return 0
endfunction

function! ExecuteMacroOverVisualRange()
  echo "@".getcmdline()
  execute ":'<,'>normal @".nr2char(getchar())
endfunction

" }} // namespace functions
